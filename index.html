<!DOCTYPE html>
<html lang="">
<head>
    <meta charset="UTF-8">
    <title>WebGL Marching Cubes</title>
    <script src="lib/jquery/js/jquery-1.10.2.min.js"></script>
    <style>
        div, body {
            margin: 0;
        }
        
        canvas {
            width: 100%;
            height: 100%;
            margin: 0;
        }
    </style>
</head>

<body>
    <script type="x-shader/x-vertex" id="mainVertexShader">
        uniform vec3 ambient;
        uniform vec3 diffuse;
        uniform vec3 emissive;
        uniform vec3 ambientLightColor;
        uniform vec3 directionalLightColor;
        uniform vec3 directionalLightDirection;
        uniform mat4 shadowMatrix;  
        varying vec4 vShadowCoord;
        varying vec3 vColor;
        varying vec3 vLightFront;

        void main()
        {
            vec3 transformedNormal = normalMatrix * normal;
            vLightFront = vec3(0.0);
            transformedNormal = normalize(transformedNormal);
            vec4 lDirection = viewMatrix * vec4(directionalLightDirection, 0.0);
            vec3 dirVector = normalize(lDirection.xyz);
            float dotProd = dot(transformedNormal, dirVector);
            vec3 directionalLightWeight = vec3(max(dotProd, 0.0));
            vLightFront += directionalLightColor * directionalLightWeight;
            vLightFront = vLightFront * diffuse + ambient * ambientLightColor + emissive;
                
            vec4 mvPosition = modelViewMatrix * vec4(position, 1.0);
            vec4 worldPosition = modelMatrix * vec4(position, 1.0);
            gl_Position = projectionMatrix * mvPosition;
        
            vShadowCoord = shadowMatrix * worldPosition;
            
            vColor = color;
        }     
    </script>
    <script type="x-shader/x-fragment" id="mainFragmentShader">
        uniform sampler2D shadowMap;
        uniform vec2 shadowMapSize;
        uniform float shadowDarkness;
        varying vec4 vShadowCoord;
        varying vec3 vColor;
        varying vec3 vLightFront;

        float unpackDepth(const in vec4 rgba_depth) {
            const vec4 bit_shift = vec4( 1.0 / ( 256.0 * 256.0 * 256.0 ), 1.0 / ( 256.0 * 256.0 ), 1.0 / 256.0, 1.0 );
            float depth = dot(rgba_depth, bit_shift);
            return depth;
        }

        void main()
        {
            vec3 shadowColor = vec3( 1.0 );
            gl_FragColor = vec4(1.0);
            gl_FragColor.xyz *= vLightFront;
            gl_FragColor = gl_FragColor * vec4( vColor, 1.0 );

            vec3 shadowCoord = vShadowCoord.xyz / vShadowCoord.w;
            
            bvec4 inFrustumVec = bvec4(shadowCoord.x >= 0.0, shadowCoord.x <= 1.0, shadowCoord.y >= 0.0, shadowCoord.y <= 1.0);
            bool inFrustum = all(inFrustumVec);
            bvec2 frustumTestVec = bvec2(inFrustum, shadowCoord.z <= 1.0);
            bool frustumTest = all(frustumTestVec);
            if (frustumTest) {            
                shadowCoord.z += 0.000005;

                vec4 rgbaDepth = texture2D(shadowMap, shadowCoord.xy);
                float distanceFromLight = unpackDepth(rgbaDepth);
                if (distanceFromLight < shadowCoord.z) {
                    shadowColor = shadowColor * vec3(1.0 - shadowDarkness);
                }

                gl_FragColor.xyz = gl_FragColor.xyz * shadowColor;
            }
        }
    
    </script>
    <script type="x-shader/x-vertex" id="shadowVertexShader">
        
        void main() {
            vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
            gl_Position = projectionMatrix * mvPosition;
        }
    
    </script>
    <script type="x-shader/x-fragment" id="shadowFragmentShader">
        vec4 pack_depth( const in float depth ) {
            const vec4 bit_shift = vec4( 256.0 * 256.0 * 256.0, 256.0 * 256.0, 256.0, 1.0 );
            const vec4 bit_mask  = vec4( 0.0, 1.0 / 256.0, 1.0 / 256.0, 1.0 / 256.0 );
            vec4 res = fract( depth * bit_shift );
            res -= res.xxyz * bit_mask;
            return res;
        }
        void main() {
            gl_FragData[ 0 ] = pack_depth( gl_FragCoord.z );
        }
    </script>
    <script type="x-shader/x-fragment" id="mainFragmentShaderPCF">
    
        uniform sampler2D shadowMap;
        uniform vec2 shadowMapSize;
        uniform float shadowDarkness;
        varying vec4 vShadowCoord;
        varying vec3 vColor;
        varying vec3 vLightFront;

        float unpackDepth(const in vec4 rgba_depth) {
            const vec4 bit_shift = vec4( 1.0 / ( 256.0 * 256.0 * 256.0 ), 1.0 / ( 256.0 * 256.0 ), 1.0 / 256.0, 1.0 );
            float depth = dot(rgba_depth, bit_shift);
            return depth;
        }

        void main()
        {
            vec3 shadowColor = vec3( 1.0 );
            gl_FragColor = vec4(1.0);
            gl_FragColor.xyz *= vLightFront;
            gl_FragColor = gl_FragColor * vec4( vColor, 1.0 );

            vec3 shadowCoord = vShadowCoord.xyz / vShadowCoord.w;
            
            bvec4 inFrustumVec = bvec4(shadowCoord.x >= 0.0, shadowCoord.x <= 1.0, shadowCoord.y >= 0.0, shadowCoord.y <= 1.0);
            bool inFrustum = all(inFrustumVec);
            bvec2 frustumTestVec = bvec2(inFrustum, shadowCoord.z <= 1.0);
            bool frustumTest = all(frustumTestVec);
            if (frustumTest) {            
                shadowCoord.z += 0.000005;

                float shadow = 0.0;
                const float shadowDelta = 1.0 / 9.0;
                float xPixelOffset = 0.0625 / shadowMapSize.x;
                float yPixelOffset = 0.0625 / shadowMapSize.y;
                float dx0 = -1.25 * xPixelOffset;
                float dy0 = -1.25 * yPixelOffset;
                float dx1 = 1.25 * xPixelOffset;
                float dy1 = 1.25 * yPixelOffset;
                float fDepth = unpackDepth( texture2D( shadowMap, shadowCoord.xy + vec2( dx0, dy0 ) ) );
                if ( fDepth < shadowCoord.z ) shadow += shadowDelta;
                fDepth = unpackDepth( texture2D( shadowMap, shadowCoord.xy + vec2( 0.0, dy0 ) ) );
                if ( fDepth < shadowCoord.z ) shadow += shadowDelta;
                fDepth = unpackDepth( texture2D( shadowMap, shadowCoord.xy + vec2( dx1, dy0 ) ) );
                if ( fDepth < shadowCoord.z ) shadow += shadowDelta;
                fDepth = unpackDepth( texture2D( shadowMap, shadowCoord.xy + vec2( dx0, 0.0 ) ) );
                if ( fDepth < shadowCoord.z ) shadow += shadowDelta;
                fDepth = unpackDepth( texture2D( shadowMap, shadowCoord.xy ) );
                if ( fDepth < shadowCoord.z ) shadow += shadowDelta;
                fDepth = unpackDepth( texture2D( shadowMap, shadowCoord.xy + vec2( dx1, 0.0 ) ) );
                if ( fDepth < shadowCoord.z ) shadow += shadowDelta;
                fDepth = unpackDepth( texture2D( shadowMap, shadowCoord.xy + vec2( dx0, dy1 ) ) );
                if ( fDepth < shadowCoord.z ) shadow += shadowDelta;
                fDepth = unpackDepth( texture2D( shadowMap, shadowCoord.xy + vec2( 0.0, dy1 ) ) );
                if ( fDepth < shadowCoord.z ) shadow += shadowDelta;
                fDepth = unpackDepth( texture2D( shadowMap, shadowCoord.xy + vec2( dx1, dy1 ) ) );
                if ( fDepth < shadowCoord.z ) shadow += shadowDelta;
                shadowColor = shadowColor * vec3( ( 1.0 - shadowDarkness * shadow ) );


                
                gl_FragColor.xyz = gl_FragColor.xyz * shadowColor;
            }
        }
        
    </script>
    <div id="container">
        <!--<canvas id="three-viewport" style="width: 100%; height: 100%"></canvas>-->
    </div>
</body>
</html>

<script src="js/marchingcubes.js"></script>
<script src="js/marchingalg.js"></script>
<script src="lib/noise/noise.js"></script>
<script src="lib/threejs/three.js"></script>
<script src="lib/threejs/UnpackDepthRGBAShader.js"></script>
<script src="js/shadows.js"></script>
<script src="js/DragControls.js"></script>
<script src="js/voxelscene.js"></script>
<script src="js/main.js"></script>
